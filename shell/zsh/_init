#!/usr/bin/env zsh
source ${0:A:h}/../../deploy
source .zshenv

# Dependencies

common_deps=(
  zsh
  fzf
  fasd
)

brew_deps=(
  zsh-history-substring-search
  fd
)

linux_deps=(
  fd-find
  make
)

install() {
  case $(_os) in
    macos)
      brew install ${common_deps[@]} ${brew_deps[@]}
      ;;
    debian)
      sudo apt install -y ${common_deps[@]} ${linux_deps[@]}

      curl https://github.com/sharkdp/fd/releases/download/v7.3.0/fd-musl_7.3.0_amd64.deb -Lo /tmp/fd-musl_7.3.0_amd64.deb
      sudo dpkg -i "/tmp/fd-musl_7.3.0_amd64.deb"
      ;;
    fedora)
      sudo dnf install -y ${common_deps[@]} ${linux_deps[@]}
      ;;
  esac

  [[ $SHELL == */zsh ]] || chsh -s $(which zsh) $(whoami)

  local zsh="$(which zsh)"
  grep $zsh /etc/shells &>/dev/null || sudo tee -a /etc/shells <<<$zsh
  sudo tee $zshenv <<<"export ZDOTDIR=\"${ZDOTDIR/$HOME/\$HOME}\""
}

update() {
  # Update zpugins and recompile
  if [[ -e ${ZPLGM[BIN_DIR]}/zplugin.zsh ]]; then
    source ${ZPLGM[BIN_DIR]}/zplugin.zsh
    zplugin self-update
    zplugin update --all
    zplugin compile --all
  fi

  _upgrade
}

link() {
  mklink .zsh* $ZDOTDIR/
  mklink functions $ZDOTDIR/

  case $(_os) in
    macos)
      mklink .zshenv ~/.zshenv
      ;;
  esac
}

clean() {
  rm -rfv "$ZDOTDIR" "$ZSH_CACHE/*";

  case $(_os) in
    macos)
      rm -f "~/.zshenv"
      ;;
  esac
}

_upgrade() {
  case $(_os) in
    macos)
      brew upgrade ${common_deps[@]} ${brew_deps[@]}
      ;;
    debian)
      sudo apt update && sudo apt upgrade -y ${common_deps[@]} ${linux_deps[@]}
      ;;
  esac
}

init "$@"
