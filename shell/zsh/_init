#!/usr/bin/env zsh
source ${0:A:h}/../../deploy
source .zshenv

# Dependencies

common_deps=(
  zsh
  fasd
)

brew_deps=(
  coreutils
  zsh-history-substring-search
  fd
)

linux_deps=(
  make
)

install() {
  case $(_os) in
    macos)
      brew install ${common_deps[@]} ${brew_deps[@]}

      zshenv=/etc/zshenv
      ;;
    debian)
      sudo add-apt-repository ppa:aacebedo/fasd -y
      sudo apt update && sudo apt install -y ${common_deps[@]} ${linux_deps[@]}

      # Need to install fd manually because it's missing in apt for ubuntu 18.04
      curl https://github.com/sharkdp/fd/releases/download/v7.3.0/fd-musl_7.3.0_amd64.deb -Lo /tmp/fd-musl_7.3.0_amd64.deb
      sudo dpkg -i "/tmp/fd-musl_7.3.0_amd64.deb"

      zshenv=/etc/zsh/zshenv
      ;;
  esac

  [[ $SHELL == */zsh ]] || sudo chsh -s $(which zsh) $(whoami)

  local zsh="$(command -v zsh)"
  grep $zsh /etc/shells &>/dev/null || sudo tee -a /etc/shells <<<$zsh
  sudo tee $zshenv <<<"export ZDOTDIR=\"${ZDOTDIR/$HOME/\$HOME}\""

  exec $zsh
}

update() {
  # Update zpugins and recompile
  if [[ -e ${ZPLGM[BIN_DIR]}/zplugin.zsh ]]; then
    source ${ZPLGM[BIN_DIR]}/zplugin.zsh
    zplugin self-update
    zplugin update --all
    zplugin compile --all
  fi

  _upgrade
}

link() {
  mklink .zsh* $ZDOTDIR/
  mklink functions $ZDOTDIR/

  case $(_os) in
    macos)
      mklink .zshenv ~/.zshenv
      ;;
  esac
}

clean() {
  rm -rfv "$ZDOTDIR" "$ZSH_CACHE/*";

  case $(_os) in
    macos)
      rm -f "~/.zshenv"
      ;;
  esac

  # Go back to bash
  sudo chsh -s $(command -v bash) $(whoami)
  exec bash
}

_upgrade() {
  case $(_os) in
    macos)
      brew upgrade ${common_deps[@]} ${brew_deps[@]}
      ;;
    debian)
      sudo apt update && sudo apt upgrade -y ${common_deps[@]} ${linux_deps[@]}
      ;;
  esac
}

init "$@"
